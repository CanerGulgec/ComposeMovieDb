plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-parcelize'
}

android {
    compileSdk 31
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.example.movieitemcompose"
        minSdk 21
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        def secureProps = new Properties()
        if (file("../secure.properties").exists()) {
            file("../secure.properties")?.withInputStream { secureProps.load(it) }
        }

        buildConfigField("Integer", "TIMEOUT", "60")
        buildConfigField("String", "BASE_URL", "\"https://api.themoviedb.org/3/\"")
        buildConfigField("String", "API_KEY", (secureProps.getProperty("MOVIE_API_KEY") ?: ""))
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
        useIR = true
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
        kotlinCompilerVersion kotlin_version
    }
}

kapt {
    correctErrorTypes true
}

dependencies {

    // Jetpack
    implementation "androidx.core:core-ktx:$core_ktx"
    implementation "androidx.appcompat:appcompat:$appcompat"
    implementation "com.google.android.material:material:$materialDesign"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_runtime"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_viewmodel"
    implementation "androidx.datastore:datastore-preferences:$dataStore"

    // Dagger - Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"
    implementation "androidx.hilt:hilt-navigation-compose:$hilt_navigation"

    // Network
    implementation "com.squareup.retrofit2:retrofit:$retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit"
    implementation "com.squareup.okhttp3:okhttp:$okhttp"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp"

    // Compose
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-util:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.activity:activity-compose:$activity_compose"
    implementation "androidx.paging:paging-compose:$paging"
    implementation "androidx.navigation:navigation-compose:$navigation"
    implementation "androidx.constraintlayout:constraintlayout-compose:$constraintlayout"

    // Accompanist
    implementation "com.google.accompanist:accompanist-flowlayout:$flowlayout"
    implementation "com.google.accompanist:accompanist-systemuicontroller:$ui_controller"
    implementation "com.google.accompanist:accompanist-pager:$pager"

    // Splash Screen API
    implementation "androidx.core:core-splashscreen:1.0.0-alpha02"

    // Image Loading
    implementation "io.coil-kt:coil-compose:$coil"
    implementation "com.github.skydoves:landscapist-glide:$landscapist_glide"

    // App Debugging
    debugImplementation "com.facebook.flipper:flipper:$flipper"
    debugImplementation "com.facebook.soloader:soloader:$soloader"
    debugImplementation "com.facebook.flipper:flipper-network-plugin:$flipper"

    // Testing
    testImplementation "junit:junit:$junit"
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
}